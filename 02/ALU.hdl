// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
//CHIP Or16Way {
//    IN in[16];
//    OUT out;
//
//   PARTS:
//        Or8Way(in=in[0..7], out=out1);
//        Or8Way(in=in[8..15], out=out2);
//        Or(a=out1, b=out2, out=out);
//}

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x[0..15], b=false, sel=zx, out=x1); 
    Not16(in=x1, out=notX);
    Mux16(a=x1, b=notX, sel=nx, out=x2); 

    Mux16(a=y[0..15], b=false, sel=zy, out=y1); 
    Not16(in=y1, out=notY);
    Mux16(a=y1, b=notY, sel=ny, out=y2); 

    Add16(a=x2, b=y2, out=xysum);   
    And16(a=x2, b=y2, out=xyand);  
    Mux16(a=xyand, b=xysum, sel=f, out=out1);
    Not16(in=out1, out=notOutput);  
    Mux16(a=out1, b=notOutput, sel=no, out=out2);
 
    And16(a=out2, b=true, out[15]=ng);
   
    And16(a=out2, b=out2, out[0..7]=outPartA, out[8..15]=outPartB);
    Or8Way(in=outPartA, out=temp1);
    Or8Way(in=outPartB, out=temp2);
    Or(a=temp1, b=temp2, out=zero);
    Not(in=zero, out=zr);       

    And16(a=out2, b=out2, out=out); 
}